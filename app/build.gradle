plugins {
    alias(libs.plugins.android.application)

}

android {
    namespace 'com.ace77505.watchface.firefly'
    compileSdk {
        version = release(36)
    }

    defaultConfig {
        applicationId "com.ace77505.watchface.firefly"
        minSdk 28
        targetSdk 36
        versionCode 1
        versionName "1.0"

    }
    signingConfigs {
        release {
            enableV1Signing = true
            enableV2Signing = true
            enableV3Signing = true
        }
    }

    File propFile = rootProject.file('signing.properties')
    if (propFile.exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(propFile))
        if (props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
            signingConfigs.release.storeFile = rootProject.file(props['STORE_FILE'])
            signingConfigs.release.storePassword = props['STORE_PASSWORD']
            signingConfigs.release.keyAlias = props['KEY_ALIAS']
            signingConfigs.release.keyPassword = props['KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
        }
    } else {
        android.buildTypes.release.signingConfig = null
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def fileName = "Firefly-${variant.versionName}-release.apk"
                output.outputFileName = fileName
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    useLibrary('wear-sdk')
}

dependencies {
//    implementation libs.play.services.wearable
//    implementation platform(libs.compose.bom)
//    implementation libs.ui
//    implementation libs.ui.graphics
//    implementation libs.ui.tooling.preview
//    implementation libs.compose.material
//    implementation libs.compose.foundation
//    implementation libs.wear.tooling.preview
//    implementation libs.activity.compose
//    implementation libs.core.splashscreen
    implementation "androidx.wear.watchface:watchface:1.1.1"
    implementation "androidx.wear.watchface:watchface-complications-rendering:1.1.1"
    implementation "androidx.wear.watchface:watchface-complications-data-source:1.1.1"

//    androidTestImplementation platform(libs.compose.bom)
//    androidTestImplementation libs.ui.test.junit4
//    debugImplementation libs.ui.tooling
//    debugImplementation libs.ui.test.manifest
}